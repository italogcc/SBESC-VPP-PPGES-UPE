// Universidade de Pernambuco - UPE
// Escola Politécnica de Pernambuco - POLI
// Programa de Pós Graduação Engenharia de Sistemas - PPGES
// Aluno: Italo Garcia Campos do Canto
// Tema: Plataforma Computacional em OpenDSS para simulação de Agregadores
//		e Usinas Virtuais Urbanas
//		Recife-PE, 2022

// Limpa a memoria do OpenDSS
clear

// Equivalente de Thevenin
New Circuit.Fonte phases=3 basekv=0.38 bus1=A

// Linhas
New LineCode.Arranjo nphases=3 basefreq=60 units=km
~ Rmatrix=[ 0.17 | 0.05 0.17 | 0.05 0.05 0.17 ]  ! ohm/km ! 
~ Xmatrix=[ 0.79 | 0.41 0.79 | 0.41 0.45 0.79 ]  ! ohm/km
~ Cmatrix=[ 8.63 | -2.04 8.75 | -0.90 -2.04 8.41 ]  ! nF/km

New Line.LinhaDistrib phases=3 bus1=A bus2=B length=0.2 units=km linecode=Arranjo
New Line.LinhaA phases=3 bus1=B bus2=C length=0.15 units=km linecode=Arranjo

// Insercao de "EnergyMeter"
New energymeter.medidor element=line.LinhaDistrib terminal=1

// Definicao da carga
New loadShape.CargaSemana npts=24 interval=1 mult=(file=CargaSemana.csv)
New Load.CargaDistrib phases=3 conn=wye bus1=B kw=40 pf=0.92 kv=0.38 daily=CargaSemana

redirect ad-01.dss
redirect gd-01.dss

Set voltagebases = [0.38]
calcVoltagebases

New monitor.LinhaDistrib_potencia element=line.LinhaDistrib terminal=1 mode=1 ppolar=no
New monitor.LinhaDistrib_tensao element=line.LinhaDistrib terminal=1 mode=0

New monitor.linhaA_potencia element=line.LinhaA terminal=2 mode=1 ppolar=no
New monitor.linhaA_tensao element=line.LinhaA terminal=2 mode=0

New monitor.carga_potencia element=load.CargaDistrib terminal=1 mode=1 ppolar=no
New monitor.carga_tensao element=load.CargaDistrib terminal=1 mode=0

set mode = daily
set stepsize = 1h
set number = 48
set datapath=[D:\Projetos\opendss\SBESC-VPP-PPGES-UPE\saida]

solve

show voltage elements
show power elements

Export monitors LinhaDistrib_potencia
Plot monitor object=LinhaDistrib_potencia channels=(1 3 5)
Export monitors LinhaDistrib_tensao
Plot monitor object=LinhaDistrib_tensao channels=(1 3 5)

Export monitors linhaA_potencia
Plot monitor object=linhaA_potencia channels=(1 3 5)
Export monitors linhaA_tensao
Plot monitor object=linhaA_tensao channels=(1 3 5)

Export monitors carga_potencia
Plot monitor object=carga_potencia channels=(1 3 5)
Export monitors carga_tensao
Plot monitor object=carga_tensao channels=(1 3 5)

